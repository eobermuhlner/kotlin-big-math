plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'maven'
    id 'signing'
}

group = 'ch.obermuhlner'
version = '2.3.0'
archivesBaseName = 'kotlin-big-math'

sourceCompatibility = 1.8

ext {
    moduleName = "ch.obermuhlner.math.big.kotlin"

    if (!project.hasProperty("ossrhUsername")) {
        ossrhUsername = "undefined"
    }
    if (!project.hasProperty("ossrhPassword")) {
        ossrhPassword="undefined"
    }
}

repositories {
    mavenLocal()
	mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation group: 'ch.obermuhlner', name: 'big-math', version: '2.3.0'

    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required {
        gradle.taskGraph.hasTask("uploadArchives")
    }

    sign configurations.archives
}

uploadArchives {
	repositories {
	    mavenDeployer {
	        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	
	        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }
	
	        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }
	
	        pom.project {
	            name 'Kotlin Big Math'
	            packaging 'jar'
	            // optionally artifactId can be defined here 
	            description 'Kotlin Math functions for BigDecimal.'
	            url 'https://github.com/eobermuhlner/kotlin-big-math'
	
	            scm {
	                connection 'scm:git:git://github.com/kotlin-big-math.git'
	                developerConnection 'scm:ssh://github.com:eobermuhlner/kotlin-big-math.git'
	                url 'https://github.com/eobermuhlner/kotlin-big-math/'
	            }
	
	            licenses {
	                license {
	                    name 'MIT License'
	                    url 'https://raw.githubusercontent.com/eobermuhlner/kotlin-big-math/master/LICENSE.txt'
	                }
	            }
	
	            developers {
	                developer {
	                    id 'eobermuhlner'
	                    name 'Eric Obermuhlner'
	                    email 'eobermuhlner@gmail.com'
	                }
	            }
	        }
	    }
	}
}
